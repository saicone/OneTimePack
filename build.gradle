plugins {
    id 'java'
    id 'com.gradleup.shadow' version libs.versions.shadow apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.gradleup.shadow'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        if (project.name != 'common') {
            implementation project(':common')
        }
        compileOnly 'org.jetbrains:annotations:26.0.2'
        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    jar {
        if (project.name.startsWith('bungeecord-') || project.name.startsWith('velocity-')) {
            dependsOn (shadowJar)
        }
    }

    shadowJar {
        archiveBaseName.set(rootProject.name + '-' + project.name)
        archiveClassifier.set('')
        destinationDirectory.set(file(rootProject.layout.buildDirectory.dir('libs')))
        if (project.name != 'bungeecord-protocolize') {
            exclude 'mappings.json'
        }
        minimize()
    }
}

jar {
    enabled = false
}